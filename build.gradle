
import org.apache.tools.ant.DirectoryScanner

plugins {
	id 'java'
	id 'org.jetbrains.intellij' version '0.4.18'
}

group 'com.adacore'
version '0.6-dev'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.6.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

intellij {
	version '2020.1'
	updateSinceUntilBuild false
}

publishPlugin {

	// TODO: Find a permanent solution that does not require
	//       commenting/uncommenting the token assignment line
	//       below every time

	// To publish a new version of the plugin:
	// - Put publish token as `publishToken` in `gradle.properties`
	// - Uncomment the line below
	// - Run the `publishPlugin` gradle task

	// token publishToken

}

sourceSets {
	main.java.srcDirs = [ 'src/main/control' , 'src/main/ui' ]
	test.java.srcDirs = [ 'src/test/control' , 'src/test/ui' ]
}

test {
	useJUnitPlatform()
}

// Removes default `.gitignore` file exclusion rule for the `processResources`
// task. This temporary solution is necessary since some of the plugin resource
// files are `.gitignore` files, which would otherwise not be included in the
// final plugin JAR. For more information, see:
// https://intellij-support.jetbrains.com/hc/en-us/community/posts/360002399679-Hidden-files-not-included-in-JAR
// https://github.com/gradle/gradle/issues/2986
processResources {

	doFirst {
		DirectoryScanner.removeDefaultExclude("**/.gitignore")
	}

}
